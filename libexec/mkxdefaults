#!/usr/bin/env perl
#
# mkxdefaults - makes .Xdefaults. the larger terminal font sizes are for
# my 1366x768 OpenBSD laptop so may need adjustment elsewhere. also more
# or less assumes tmux(1) (or games) given the lack of scrolling options
#
# see also appres(1)

use 5.24.0;
use warnings;
use Carp 'croak';

print <<'HEADER';
! built with mkxdefaults

*locale:UTF-8

mupdf*pointerMode:1

URxvt*foreground:white
URxvt*background:black
URxvt*boldFont:xft:Cousine:pixelsize=26:bold
URxvt*boldItalicFont:xft:Cousine:pixelsize=26:bold:italic
URxvt*font:xft:Cousine:pixelsize=26
URxvt*italicFont:xft:Cousine:pixelsize=26:italic
URxvt*color0:#000000
URxvt*color1:#A80000
URxvt*color2:#00A800
URxvt*color3:#A8A800
URxvt*color4:#0000A8
URxvt*color5:#A800A8
URxvt*color6:#00A8A8
URxvt*color7:#A8A8A8
URxvt*color8:#000054
URxvt*color9:#FF0054
URxvt*color10:#00FF54
URxvt*color11:#FFFF54
URxvt*color12:#0000FF
URxvt*color13:#FF00FF
URxvt*color14:#00FFFF
URxvt*color15:#FFFFFF
URxvt*loginShell:true
URxvt*pointerBlank:true
URxvt*saveLines:0
URxvt*scrollBar:false
HEADER

# configure XTerm in various guises; see also tty/bigterm in the scripts
# repo and cwmrc.m4 for related autogroup foo
my $defaults = {
    background      => "black",
    colorMode       => "false",
    cursorBlink     => "true",
    cursorColor     => "NavajoWhite",
    cursorOffTime   => "100",
    cursorOnTime    => "1000",
    dynamicColors   => "false",
    foreground      => "white",
    logInhibit      => "true",
    loginShell      => "true",
    pointerMode     => "1",
    saveLines       => "0",
    scrollBar       => "false",
    trimSelection   => "true",
    visualBell      => "true",
    visualBellDelay => "0",
};

my $cous = { faceName => "Cousine", boldFont => "Cousine-Bold" };

# color: only for games
my $fancy = {
    colorMode     => "true",
    dynamicColors => "true",
    termName      => "xterm-256color"
};

# NOTE "White Rabbit" may appear as "New" in font listings
#
#   $ fc-list | awk -F/ '/Rabbit/{print $NF}'
#   whitrabt.ttf: White Rabbit,New:style=Regular
my $wr = { faceName => "White Rabbit" };

my @terminals = (
    [ xterm => [] ],
    [ cousterm => [ $cous, $fancy, { faceSize => 19 } ],
      "embiggened for DCSS and walls of text" ],
    [ cous2term => [ $cous, $fancy, { faceSize => 32 } ] ],
    [ mfterm => [ $fancy, { faceName => "monofur", faceSize => 20 } ],
      "Comic Sans MS being the good thing to come out of Redmond" ],
    [ wrterm => [ $wr, { faceSize => 24, geometry => "80x24" } ],
      q{"White Rabbit" square font} ],
);

for my $tent (@terminals) {
    print terminal(
        $tent->[0],
        with => [ $defaults, @{ $tent->[1] } ],
        $tent->[2] ? (note => $tent->[2]) : ()
    );
}

sub resources {
    my $resources;
    for my $rset (@_) {
        for my $key (keys %$rset) {
            $resources->{$key} = $rset->{$key};
        }
    }
    return $resources;
}

sub terminal {
    my ($class, %param) = @_;
    croak "no resources for $class"
      unless defined $param{with}
      and ref $param{with} eq 'ARRAY';
    my $resources = resources @{ $param{with} };
    my $string    = "\n";
    $string .= "! $param{note}\n" if $param{note};
    for my $key (sort keys %$resources) {
        $string .= "$class*$key:$resources->{$key}\n";
    }
    return $string;
}
