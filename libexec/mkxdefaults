#!/usr/bin/env perl
#
# mkxdefaults - makes .Xdefaults. font sizes are aimed at my 1366x768
# OpenBSD laptop so will need adjustment elsewhere

use 5.24.0;
use warnings;

my $defaults = {
    background        => "black",
    colorMode         => "false",
    cursorBlink       => "false",
    cursorColor       => "white",
    dynamicColors     => "false",
    foreground        => "gold",
    keepSelection     => "true",
    loginShell        => "true",
    pointerMode       => "1",
    saveLines         => "0",
    scrollBar         => "false",
    selectToClipboard => "true",
    titleInhibit      => "true",
    visualBell        => "true",
    visualBellDelay   => "0",
};

# really only for games, not for productive work
my $fancy = {
    colorMode     => "true",
    dynamicColors => "true",
    termName      => "xterm-256color",
};

sub emit_attr {
    my ($type, $attr, $comment) = @_;
    my $s = '';
    $s .= '# ' . $comment . "\n" if defined $comment and length $comment;
    for my $key (sort keys $attr->%*) {
        $s .= "${type}term" . '*' . $key . ':' . $attr->{$key} . "\n";
    }
    say $s;
}

sub merge_attr {
    my $attr;
    for my $from (@_) {
        for my $key (keys $from->%*) {
            $attr->{$key} = $from->{$key};
        }
    }
    return $attr;
}

print <<'HEADER';
# built with mkxdefaults

*locale:                UTF-8

HEADER

emit_attr("x", $defaults);
emit_attr("cous",
    merge_attr($defaults, $fancy, { faceName => "Cousine-Bold", faceSize => 18 }));
emit_attr(
    "excel",
    merge_attr(
        $defaults, $fancy, { faceName => "Fixedsys Excelsior 3.01:pixelsize=28" }
    )
);
emit_attr("mf",
    merge_attr($defaults, $fancy, { faceName => "monofur", faceSize => 20 }));
emit_attr("spleen",
    merge_attr($defaults, $fancy, { faceName => "spleen", faceSize => 18 }));

# brogue needs embiggened terminal
emit_attr(
    "brogue",
    merge_attr(
        $defaults, $fancy,
        { faceName => "Cousine:pixelsize=18", geometry => "100x34" }
    )
);

# two-thirds of the screen (where the other 1/3 is two of the
# regular xterms)
emit_attr(
    "tt",
    merge_attr(
        $defaults, $fancy,
        { faceName => "Cousine-Bold", faceSize => 14, geometry => "80x33" }
    )
);

# for playing rogue in (minimum dimensions)
emit_attr(
    "wr",
    merge_attr(
        $defaults, $fancy,
        { faceName => "New", faceSize => 24, geometry => "70x22" }
    ),
    q{"White Rabbit" square font}
);

print <<'FOOTER';
mupdf*pointerMode:      1

xbattery*visualBell:    false 

xclipboard*geometry:    388x200+488+53

xload*background:       black
xload*foreground:       white
xload*highlight:        darkred
xload*showLabel:        false
xload*update:           15
FOOTER
