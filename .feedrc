switch -glob $command {
    sbcl {
        proc await_prompt {} {
            expect {
                -ex "debugger invoked on a " interactionby
                -re {\* $} {}
            }
        }
        proc consume {fh} {
            global buf
            while {[gets $fh line] >= 0} {
                if {[regexp {^[[:blank:]]*(?:;|$)} $line]} { continue }
                if {[regexp {^\(} $line]} {
                    if {[string length $buf]} { send -- "$buf\r" }
                    set buf $line
                    return 1
                } else {
                    append buf " " [string trim $line]
                }
            }
            return 0
        }
    }

    expect  -
    tclsh*  -
    wish*   {
        proc await_prompt {} {
            # default "% " prompt or I've instead set tcl_prompt1
            # to "foo> " also do not match expect's "+>"
            # continuation prompts
            expect -re {[^+][%>] }
        }
        proc consume {fh} {
            global buf
            while {[gets $fh line] >= 0} {
                # TCL allows # or ;# as comments though the latter is
                # usually only after some statement that must be noted
                # as ended
                if {[regexp {^[[:blank:]]*(?:;?#|$)} $line]} { continue }
                if {[regexp {[\\\{]$} $line]} {
                    if {[string length $buf]} { send -- "$buf\r"; set buf {} }
                    append buf $line
                    continue
                } elseif {[regexp {^[[:blank:]]} $line]} {
                    append buf " " [string trim $line] ";"
                    continue
                } elseif {[regexp {^\}} $line]} {
                    append buf $line
                    return 1
                } else {
                    if {[string length $buf]} { send -- "$buf\r" }
                    set buf $line
                    return 1
                }
            }
            return 0
        }
    }

    egdb -
    gdb  {
        # will need adjustment if `set prompt` has been customized in
        # gdb; '>' is for "Breakpoint Command Lists" continuation lines
        proc await_prompt {} { expect -re {\(gdb|>} }
    }
}
